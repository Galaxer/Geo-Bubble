
// Build config for all feature modules
project.subprojects { subproject ->

    // Apply plugin for all modules
    apply plugin: 'com.android.library'
    apply plugin: 'org.jetbrains.kotlin.android'
    apply plugin: 'kotlin-kapt'
    apply plugin: 'com.google.dagger.hilt.android'

    // Android config for all modules
    android {
        compileSdk 33

        defaultConfig {
            minSdk 25
            targetSdk 33

            testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
            consumerProguardFiles "consumer-rules.pro"
        }

        buildTypes {
            release {
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            }
        }

        sourceSets {
            test {
                java.srcDirs += project(":ui").file("src/test/java")
            }
        }

        buildFeatures {
            compose true
        }

        composeOptions {
            kotlinCompilerExtensionVersion libs.versions.compose.compiler.get()
        }

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }

        kotlinOptions {
            jvmTarget = '1.8'
        }
    }

    // Dependencies for all feature modules
    dependencies {

        // Modules
        implementation project(":ui")
        implementation project(":domain")
        testImplementation project(":testUtils")

        // DI
        implementation "com.google.dagger:hilt-android:${libs.versions.hilt.get()}"
        kapt "com.google.dagger:hilt-compiler:${libs.versions.hilt.get()}"
        implementation "androidx.hilt:hilt-navigation-compose:${libs.versions.compose.hilt.nav.get()}"

        // AndroidX Kotlin extensions
        implementation "androidx.core:core-ktx:${libs.versions.core.ktx.get()}"

        // Compose
        implementation "androidx.compose.ui:ui:${libs.versions.compose.ui.get()}"
        implementation "androidx.compose.ui:ui-tooling-preview:${libs.versions.compose.ui.get()}"
        implementation "androidx.compose.material3:material3:${libs.versions.compose.material3.get()}"

        // Lifecycle
        implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:${libs.versions.androidx.lifecycle.get()}"
        implementation "androidx.lifecycle:lifecycle-viewmodel-compose:${libs.versions.androidx.lifecycle.get()}"

        // Testing
        testImplementation "junit:junit:${testLibs.versions.junit.get()}"
        testImplementation "org.mockito:mockito-core:${testLibs.versions.mockito.get()}"
        testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:${libs.versions.kotlin.coroutines.get()}"

        // UI Test
        androidTestImplementation "androidx.test.ext:junit:${testLibs.versions.androidx.junit.get()}"
        androidTestImplementation "androidx.test.espresso:espresso-core:${testLibs.versions.espresso.get()}"
        androidTestImplementation "androidx.compose.ui:ui-test-junit4:${libs.versions.compose.ui.get()}"

        // Debug
        debugImplementation "androidx.compose.ui:ui-tooling:${libs.versions.compose.ui.get()}"
        debugImplementation "androidx.compose.ui:ui-test-manifest:${libs.versions.compose.ui.get()}"
    }

    // Allow references to generated code
    kapt {
        correctErrorTypes true
    }

    // Kotlin compiler args
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            freeCompilerArgs += ['-opt-in=kotlin.RequiresOptIn']
        }
    }
}
